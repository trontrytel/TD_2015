R_d = 8.31432 / 0.02896                        #
R_v = 8.31432 / 0.01802                        # 
rho_v(p, T, r_v) = p / T / (R_d / r_v + R_v)   # kg/m3
rho_H2O = 1000                                 # kg/m3

p0 = 1
p1 = 1./10000
funkcja(z) = p0 * exp(-p1 * z)
fit funkcja(x) 'dane' using 2 : (rho_v($1 * 100, $3 + 273.15, $6 / 1000)) via p0, p1

print "woda opadowa: ok. ", int(p0 / p1 * 1000 / rho_H2O), " mm"

set term x11
set xlabel "wysokość [m]"
set ylabel "wilgotność bezwzględna [kg/m^3]"
plot \
  funkcja(x) with filledcurve below x1 lt 3 t 'dopasowanie', \
  'dane' using 2 : (rho_v($1 * 100, $3 + 273.15, $6 / 1000)) with linespoints lw 4 title "sondaż" 
pause -1 
